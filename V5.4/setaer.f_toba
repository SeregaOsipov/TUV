* This file contains the following subroutine, related to setting up the
* vertical profiles of atmospheric variables
*     setaer

*=============================================================================*

      SUBROUTINE setaer(ipbl, zpbl, aod330,
     $     tau550, ssaaer, alpha,
     $     nz, z, nw, wl, 
     $     dtaer, omaer, gaer)

*-----------------------------------------------------------------------------*
*=  PURPOSE:                                                                 =*
*=  Set up an altitude profile of aerosols, and corresponding absorption     =*
*=  optical depths, single scattering albedo, and asymmetry factor.          =*
*=  Single scattering albedo and asymmetry factor can be selected for each   =*
*=  input aerosol layer (do not have to correspond to working altitude       =*
*=  grid).  See loop 27.                                                     =*
*-----------------------------------------------------------------------------*
*=  PARAMETERS:                                                              =*
*=  NZ      - INTEGER, number of specified altitude levels in the working (I)=*
*=            grid                                                           =*
*=  Z       - REAL, specified altitude working grid (km)                  (I)=*
*=  NW     - INTEGER, number of specified intervals + 1 in working        (I)=*
*=           wavelength grid                                                 =*
*=  WL     - REAL, vector of lower limits of wavelength intervals in      (I)=*
*=           working wavelength grid                                         =*
*=  DTAER   - REAL, optical depth due to absorption by aerosols at each   (O)=*
*=            altitude and wavelength                                        =*
*=  OMAER   - REAL, single scattering albedo due to aerosols at each      (O)=*
*=            defined altitude and wavelength                                =*
*=  GAER    - REAL, aerosol asymmetry factor at each defined altitude and (O)=*
*=            wavelength                                                     =*
*-----------------------------------------------------------------------------*

      IMPLICIT NONE
      INCLUDE 'params'

      INTEGER kdata
      PARAMETER(kdata=51)

* input:

      REAL wl(kw)
      REAL z(kz)
      INTEGER nz
      INTEGER nw

      REAL tau550
      REAL ssaaer, alpha

* output: (on converted grid)
      REAL dtaer(kz,kw), omaer(kz,kw), gaer(kz,kw)

* local:
      REAL zd(kdata), aer(kdata)
      REAL cd(kdata), omd(kdata), gd(kdata)
      REAL womd(kdata), wgd(kdata)

      REAL cz(kz)
      REAL omz(kz)
      REAL gz(kz)

      REAL colold

      REAL wc, wscale
      INTEGER i, iw, nd

      REAL fsum
      EXTERNAL fsum

      REAL zpbl
      INTEGER ipbl

      REAL aod330

      REAL aodw(kw), ssaw(kw)
      REAL fract(kz)

*_______________________________________________________________________

* Aerosol data from Elterman (1968)
* These are vertical optical depths per km, in 1 km
* intervals from 0 km to 50 km, at 340 nm.
* This is one option.  User can specify different data set.

* Osipov, replace the default profile with the volcanic profile.
* Obtained from ModelE sims
     
      DATA aer/
     1     1.54e-02,2.57e-02,9.70e-03,7.54e-03,6.94e-03,7.65e-03,
     2     1.02e-02,1.72e-02,2.96e-02,4.57e-02,6.47e-02,8.47e-02,
     3     1.08e-01,1.42e-01,1.86e-01,2.26e-01,2.61e-01,2.90e-01,
     4     3.07e-01,3.16e-01,3.11e-01,3.04e-01,2.96e-01,2.91e-01,
     5     2.88e-01,2.84e-01,2.76e-01,2.69e-01,2.61e-01,2.38e-01,
     6     2.05e-01,1.72e-01,1.39e-01,1.12e-01,9.58e-02,7.92e-02,
     7     6.26e-02,4.99e-02,4.18e-02,3.38e-02,2.57e-02,1.92e-02,
     8     1.54e-02,1.15e-02,7.70e-03,3.87e-03,3.16e-03,2.46e-03,
     9     1.75e-03,1.04e-03,6.10e-04/

     
     
*_______________________________________________________________________


* Altitudes corresponding to Elterman profile, from bottom to top:

      WRITE(kout,*)'aerosols:  Elterman (1968) continental profile'
      nd = 51
      DO 22, i = 1, nd
         zd(i) = FLOAT(i-1)
   22 CONTINUE

* assume these are point values (at each level), so find column
* increments

      DO 27, i = 1, nd - 1
         cd(i) = (aer(i+1) + aer(i)) / 2.
         omd(i) = ssaaer
         gd(i) = .61
   27 CONTINUE

*********** end data input.

* Compute integrals and averages over grid layers:
* for g and omega, use averages weighted by optical depth

      DO 29, i = 1, nd-1
         womd(i) = omd(i) * cd(i)
         wgd(i) = gd(i) * cd(i)
   29 CONTINUE
      CALL inter3(nz,z,cz, nd,zd,cd, 1)
      CALL inter3(nz,z,omz, nd, zd,womd, 1)
      CALL inter3(nz,z,gz , nd, zd,wgd, 1)
      DO 30, i = 1, nz-1
         IF (cz(i) .GT. 0.) THEN
            omz(i) = omz(i)/cz(i)
            gz(i)  = gz(i) /cz(i)
         ELSE
            omz(i) = 1.
            gz(i) = 0.
         ENDIF
   30 CONTINUE

* old column at 340 nm
*  (minimum value is pzero = 10./largest)

      colold = MAX(fsum(nz-1,cz),pzero)

* scale with new column tau at 550 nm

      IF(tau550 .GT. nzero) THEN
         DO i = 1, nz-1
            cz(i) = cz(i) * (tau550/colold) * (550./340.)**alpha 
         ENDDO
      ENDIF

* assign at all wavelengths
* (can move wavelength loop outside if want to vary with wavelength)

      DO 50, iw = 1, nw - 1
         wc = (wl(iw)+wl(iw+1))/2.

* Elterman's data are for 340 nm, so assume optical depth scales 
* inversely with first power of wavelength.

         wscale = (340./wc)**alpha

* optical depths:

         DO 40, i = 1, nz - 1
            dtaer(i,iw) = cz(i)  * wscale
            omaer(i,iw) = omz(i)
            gaer(i,iw) = gz(i)
   40    CONTINUE
   50 CONTINUE

*! overwrite for pbl:

      IF(ipbl .GT. 0) THEN	
         write (*,*) 'pbl aerosols, aod330 = ', aod330

* create wavelength-dependent optical depth and single scattering albedo:
	
         DO iw = 1, nw-1
            wc = (wl(iw)+wl(iw+1))/2.
            aodw(iw) = aod330*(wc/330.)**(-1.0)
            IF(wc .LT. 400.) THEN
               ssaw(iw) = 0.6
            ELSE
               ssaw(iw) = 0.9
            ENDIF	
         ENDDO

* divide aod among pbl layers, overwrite Elterman profile in pbl

         DO i = 1, ipbl
            fract(i) = (z(i+1) - z(i))/zpbl
         ENDDO
	
         DO iw = 1, nw-1
            DO i = 1, ipbl 
               dtaer(i, iw) = aodw(iw) * fract(i)
               omaer(i,iw) = ssaw(iw)
            ENDDO
         ENDDO

      ENDIF
*_______________________________________________________________________

      RETURN
      END
